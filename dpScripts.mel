proc setupNamedCommands() {
	nameCommand -annotation "Isolate Selection Toggle" -command "dpIsolateSelectionToggle()" dpIsolateSelectionToggleCommand;
	nameCommand -annotation "Old Split Polygon Tool" -command "SplitPolygonTool" dpSplitPolygonToolCommand;
	nameCommand -annotation "Deselect All" -command "select -cl;" dpDeselectAllCommand;
}

proc setupHotkeys() {
	hotkey -k "q" -alt -name "dpIsolateSelectionToggleCommand";
	hotkey -k "s" -alt -name "dpSplitPolygonToolCommand";
	hotkey -k "d" -alt -name "dpDeselectAllCommand";
}


/// ISOLATE SELECTION TOGGLE ///
global proc dpIsolateSelectionToggle()
{
	string $currPanel = `getPanel -withFocus`;
	if ("" != $currPanel && ("modelPanel" == `getPanel -to $currPanel`))
	{
		int $isolate_onoff = `isolateSelect -q -state $currPanel`;
		switch ($isolate_onoff)
		{
			case 0: 
				enableIsolateSelect $currPanel 1;
				isolateSelect -state 1 $currPanel;
				break;
			case 1: 
				enableIsolateSelect $currPanel 0;
				isolateSelect -state 0 $currPanel;
				break;
			default: break;
		}
		$isolate_onoff = `isolateSelect -q -state $currPanel`;
		// print("IsolateView: " + $isolate_onoff + "\n");
	} else {
		error "No modeling panel selected or invalid.";
	}
}


global proc dpScriptsInit()
{
	print "... loading dpScripts ...";

	// To show file extensions in filter settings
	optionVar -iv FileDialogHideExtension false;

	// Increase undo queue size
	// Window > Settings/Preferences > Preferences > Settings > Undo
	undoInfo -length 200;

	// hotBox transparency to 0%
	// Menu: Right quarter in hotbox: Hotbox controls > Set transparency
	hotBox -tr 0;

	// Disable the interactive Polygon / NURBS creation. 
	// Menu: Create / NURBS Primitives / Interactive Creation
	optionVar -intValue createNurbsPrimitiveAsTool 0;// nurbs 
	//menuItem -e -cb 0 toggleCreateNurbsPrimitivesAsToolItem; // nurbs 
	optionVar -intValue createPolyPrimitiveAsTool 0;// poly 
	//menuItem -e -cb 0 toggleCreatePolyPrimitivesAsToolItem;// poly 

	// Remove viewcube   
	viewManip -visible off; optionVar -iv viewCubeShowCube 0;

	// More precise channel box display 
	global string $gChannelBoxName; 
	channelBox -e -precision 4 $gChannelBoxName;

	// Heads-up display 
	// Display > Heads Up Display > Poly Count 
	setPolyCountVisibility 1; 

	setupNamedCommands();
	setupHotkeys();
}